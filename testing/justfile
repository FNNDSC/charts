# namespace: "kk"
# release name: "cacao"
# these values are hard-coded and short for convenience

# Startup and Shutdown Commands
# --------------------------------------------------------------------------------

kind:
    kind create cluster --name khris --config kind-with-nodeport.yml

unkind:
    kind delete cluster --name khris

up:
    helm upgrade --create-namespace --install \
        -n kk cacao ../charts/chris \
        --set cube.ingress.nodePort=32000 --set cube.ingress.nodePortHost=$(hostname)

down:
    helm uninstall -n kk cacao
    kubectl delete ns kk

# Development Commands
# --------------------------------------------------------------------------------

wait:
    kubectl wait --for=condition=ready pod -n kk -l app.kubernetes.io/name=chris-server --timeout=300s

test:
    helm test -n kk cacao

create-user username password:
    xh :32000/api/v1/users/ username='{{ username }}' password='{{ password }}' email='{{ username }}@example.org'

init:
    ./init.sh

describe name:
    kubectl describe -n kk pod -l app.kubernetes.io/name={{ name }}

log name:
    kubectl logs -n kk -l app.kubernetes.io/name={{ name }}

loga name:
    kubectl logs -n kk -l app.kubernetes.io/name={{ name }} --all-containers

chrisomatic:
    [ -f chrisomatic.yml ] || just init
    docker run --rm -it \
        -v "$PWD/chrisomatic.yml:/chrisomatic.yml:ro" \
        -v "/var/run/docker.sock:/var/run/docker.sock" \
        ghcr.io/fnndsc/chrisomatic:0.7.1

shell:
    kubectl exec -it -n kk deploy/cacao-chris-server -- bash

observe:
    helm repo add vector https://helm.vector.dev
    helm repo add metrics-server https://kubernetes-sigs.github.io/metrics-server/
    helm repo update vector metrics-server
    helm upgrade --install --set args={--kubelet-insecure-tls} metrics-server metrics-server/metrics-server --namespace kube-system
    helm upgrade --install --create-namespace -n monitoring -f monitoring/vector-agent.yml vector-agent vector/vector
    helm upgrade --install -n monitoring -f monitoring/vector-aggregator.yml vector-aggregator vector/vectord
    kubectl apply -f monitoring/metrics-reader.yml
    helm upgrade --reuse-values -n monitoring vector-aggregator vector/vector \
        --set customConfig.sources.cadvisor.auth.token="$(kubectl create token -n monitoring --duration=99999h vector-aggregator)" \
        --set customConfig.sources.cadvisor.endpoints="$(./monitoring/_get_cadvisor_endpoints.sh)"

    kubectl apply -f monitoring/openobserve.yml
    kubectl rollout status -n monitoring statefulset openobserve
    sleep 1
    xdg-open "http://localhost:32020/web/logs?stream=k8s&period=5m&refresh=0&sql_mode=false&query=a3ViZXJuZXRlc19wb2RfbmFtZXNwYWNlPSdrayc=&org_identifier=default"

rm-observe:
    kubectl delete ns monitoring

olog name:
    ./monitoring/_get_openobserve_logs.sh '{{ name }}'

top:
    kubectl top -n kk pod --containers | grep -v POD | grep -v prometheus-proxy

# Helper Functions
# --------------------------------------------------------------------------------

get-url:
    echo "http://$(hostname):$(just get-nodeport)/api/v1/"

get-nodeport:
    kubectl get service -n kk -l app.kubernetes.io/name=chris-api-nodeport -o jsonpath='{.items[0].spec.ports[0].nodePort}'

get-su key:
    kubectl get secret -n kk -l app.kubernetes.io/name=chris-superuser -o jsonpath='{.items[0].data.{{key}}}' | base64 -d

get-value query:
    helm get values --all -o json -n kk cacao | jq -r '{{ query }}'
