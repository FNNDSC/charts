# namespace: "kk"
# release name: "cacao"
# these values are hard-coded and short for convenience

# Startup and Shutdown Commands
# --------------------------------------------------------------------------------

kind:
    kind create cluster --name khris --config kind-with-nodeport.yml

unkind:
    kind delete cluster --name khris

up:
    helm upgrade --create-namespace --install \
        -n kk cacao ../charts/chris \
        --set cube.ingress.nodePort=32000 --set cube.ingress.nodePortHost=$(hostname)

down:
    helm uninstall -n kk cacao
    kubectl delete ns kk

# Development Commands
# --------------------------------------------------------------------------------

wait:
    kubectl wait --for=condition=ready pod -n kk -l app.kubernetes.io/name=chris-server --timeout=300s

test:
    helm test -n kk cacao

test-fs:
    [ -f venv/bin/activate ] || (python -m venv venv && venv/bin/pip install aiochris)
    source venv/bin/activate && python test_fs.py

init:
    ./init.sh

chrisomatic:
    [ -f chrisomatic.yml ] || just init
    docker run --rm -it \
        -v "$PWD/chrisomatic.yml:/chrisomatic.yml:ro" \
        -v "/var/run/docker.sock:/var/run/docker.sock" \
        ghcr.io/fnndsc/chrisomatic:0.7.1

shell:
    kubectl exec -it -n kk deploy/cacao-chris-server -- bash

observe:
    [ -n "$(kubectl get ns -l kubernetes.io/metadata.name=monitoring -o name)" ] || kubectl create ns monitoring

    helm repo add vector https://helm.vector.dev
    helm repo add metrics-server https://kubernetes-sigs.github.io/metrics-server/
    helm repo update vector metrics-server
    helm upgrade --install --create-namespace -n monitoring -f monitoring/vector-values.yml vector vector/vector
    helm upgrade --install --set args={--kubelet-insecure-tls} metrics-server metrics-server/metrics-server --namespace kube-system

    kubectl apply -f monitoring/openobserve.yml
    kubectl rollout status -n monitoring statefulset openobserve
    sleep 1
    xdg-open "http://localhost:32020/web/logs?stream=k8s&period=5m&refresh=0&sql_mode=false&query=a3ViZXJuZXRlc19wb2RfbmFtZXNwYWNlPSdrayc=&org_identifier=default"

rm-observe:
    kubectl delete -f monitoring/openobserve.yml; kubectl delete -n monitoring pvc -l app=openobserve

olog name:
    ./get_openobserve_logs.sh '{{ name }}'

top:
    kubectl top -n kk pod --containers | grep -v POD | grep -v prometheus-proxy

# Helper Functions
# --------------------------------------------------------------------------------

get-url:
    echo "http://$(hostname):$(just get-nodeport)/api/v1/"

get-nodeport:
    kubectl get service -n kk -l app.kubernetes.io/name=chris-api-nodeport -o jsonpath='{.items[0].spec.ports[0].nodePort}'

get-su key:
    kubectl get secret -n kk -l app.kubernetes.io/name=chris-superuser -o jsonpath='{.items[0].data.{{key}}}' | base64 -d

get-value query:
    helm get values --all -o json -n kk cacao | jq -r '{{ query }}'
